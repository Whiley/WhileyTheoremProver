// Copyright (c) 2011, David J. Pearce (djp@ecs.vuw.ac.nz)
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//    * Redistributions of source code must retain the above copyright
//      notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//      notice, this list of conditions and the following disclaimer in the
//      documentation and/or other materials provided with the distribution.
//    * Neither the name of the <organization> nor the
//      names of its contributors may be used to endorse or promote products
//      derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL DAVID J. PEARCE BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

// ====================================================================
// Overview
// ====================================================================
//
// Defines a language of types similar to that found in this paper:
//
// * "Sound and Complete Flow Typing with Unions, Intersections and
//    Negations", David J. Pearce, In Proceedings of VMCAI, 2013.

// ====================================================================
// Not
// ====================================================================

term NotT(Type)

@rank(0)
@name("NotT(AnyT)")
reduce NotT(AnyT):
    => VoidT

@rank(0)
@name("NotT(VoidT)")
reduce NotT(VoidT):
    => AnyT

@rank(0)
@name("NotT(NotT)")
reduce NotT(NotT(Type t)):
    => t

@rank(1)
@name("NotT(OrT)")
reduce NotT(OrT{Type... es}):
    => let ns = { NotT(e) | e in es }
       in AndT(ns)

@rank(2)
@name("NotT(AndT)")
reduce NotT(AndT{Type... es}):
    => let ns = { NotT(e) | e in es }
       in OrT(ns)

// ====================================================================
// And
// ====================================================================

term AndT{Type...}

@rank(0)
@name("AndT{}")
reduce AndT{}:
    => VoidT

@rank(0)
@name("AndT{Type}")
reduce AndT{Type t}:
    => t

@rank(1)
@name("AndT{AndT,Type...}")
reduce AndT{AndT{Type... xs}, Type... ys}:
    => AndT (xs ++ ys)

@rank(3)
@name("AndT{OrT,Type...}")
reduce AndT{OrT{Type... xs}, Type... ys}:
    => let zs = { AndT(x ++ ys) | x in xs }
       in OrT (zs)

// ====================================================================
// Or
// ====================================================================

term OrT{Type...}

@rank(0) 
@name("OrT{}")
reduce OrT{}:
    => VoidT

@rank(0)
@name("OrT{Type}")
reduce OrT{Type t}:
    => t

@rank(1)
@name("OrT{OrT,Type...}")
reduce OrT{OrT{Type... xs}, Type... ys}:
    => OrT (xs ++ ys)

// ====================================================================
// Tuple
// ====================================================================
term TupleT[Type...]

@rank(0)
@name("TupleT[Type...]")
reduce TupleT[Type... ts]
requires VoidT in ts:
    => VoidT

@rank(0)
@name("TupleT[]")
reduce TupleT[Type... ts]
requires |ts| == 0:
    => VoidT

@rank(0)
@name("TupleT[Type]")
reduce TupleT[Type t]:
    => t

@rank(2)
@name("AndT{TupleT,TupleT,Type...}")
reduce AndT{TupleT[Type... t1s], TupleT[Type... t2s], Type... ts}:
    => VoidT, if |t1s| != |t2s|
    => let r = [ AndT{t1s[i],t2s[i]} | i in 0..|t1s| ]
       in AndT(TupleT(r) ++ ts)

@rank(2)
@name("AndT{TupleT,NotT(TupleT,Type...}")
reduce AndT{TupleT[Type... t1s] t1, NotT(TupleT[Type... t2s]), Type... ts}:
    => AndT(t1 ++ ts), if |t1s| != |t2s|
    => VoidT, if |t1s| == 0
    => let r = [ AndT{t1s[i],NotT(t2s[i])} | i in 0..|t1s| ]
       in AndT(TupleT(r) ++ ts)

@rank(2)
@name("AndT{TupleT,NotT(ArrayT),Type...}")
reduce AndT{TupleT[Type...] t1, NotT(ArrayT(Type)), Type... ts}:
    => AndT({t1} ++ ts)

// TODO: pull disjuncts out of tuples?

// ====================================================================
// Array
// ====================================================================
term ArrayT(Type)

// Handle the case of an invalid set type.  This rule is really what
// makes set types complicated.  The rule is necessary purely to help
// with intersection.  For example {(int,int)} & {int} =>
// {(int,int)&int}{void}. This makes sense, but is deceiving.  For
// example, {int} & !{any} => {int&!any} => {void} as well.
@rank(0)
@name("ArrayT(VoidT)")
reduce ArrayT(VoidT):
    => VoidT

@rank(2)
@name("AndT{ArrayT,ArrayT,Type...}")
reduce AndT{ArrayT(Type t1), ArrayT(Type t2), Type... ts}:
    => AndT(ArrayT(AndT{t1,t2})++ts)

@rank(2)
@name("AndT{ArrayT,NotT(ArrayT),Type...}")
reduce AndT{ArrayT(Type t1), NotT(ArrayT(Type t2)), Type... ts}:
    => AndT(ArrayT(AndT{t1,NotT(t2)})++ts)

@rank(2)
@name("AndT{ArrayT,NotT(Tuple),Type...}")
reduce AndT{ArrayT(Type) t1, NotT(TupleT[Type...]), Type... ts}:
    => AndT({t1} ++ ts)

@rank(1)
@name("AndT{ArrayT,Proton,Type...}")
reduce AndT{ArrayT(Type) s, Proton p, Type... ts}
requires !(p is AnyT):
    => VoidT

@rank(2)
@name("OrT{ArrayT,ArrayT,Type...}")
reduce OrT{ArrayT(Type t1) s1, ArrayT(Type t2) s2, Type... ts}
requires t1 == t2:
    => OrT({s1}++ts)

// ====================================================================
// Atoms
// ====================================================================

term AnyT
term VoidT
term NullT
term BoolT
term IntT
term RealT
term StringT
term VarT(string)
term NominalT(string)

define Quark as AnyT | NullT | VoidT | BoolT | IntT | RealT | StringT | VarT(string) | NominalT(string)
define Proton as Quark | TupleT[Proton...] | ArrayT(Proton)
define Atom as Proton | NotT(Proton)

@rank(0)
@name("AndT{VoidT,Type...}")
reduce AndT{VoidT, Type... xs}:
    => VoidT

@rank(0)
@name("AndT{AnyT, Type...}")
reduce AndT{AnyT, Type... xs}:
    => AndT(xs)

// a1 needs to be a quark here, as the rule for tuples is handled
// above.
@rank(1)
@name("AndT{Quark,Proton,Type...}")
reduce AndT{Quark a1, Proton a2, Type... ts}
requires a1 != a2 && a1 != AnyT && a2 != AnyT:
    => VoidT

// a1 needs to be a quark here, as the rule for tuples is handled
// above.
@rank(1)
@name("AndT{Quark,NotT(Proton),Type...}")
reduce AndT{Quark a1, NotT(Proton a2), Type... ts}
requires a1 == a2:
    => VoidT

@rank(1)
@name("AndT{Quark,NotT(Proton),Type...}")
reduce AndT{Quark a1, NotT(Proton a2), Type... ts}
requires a1 != a2 && a2 != AnyT:
    => AndT (a1 ++ ts)

@rank(0)
@name("OrT{AnyT,Type...}")
reduce OrT{AnyT, Type... xs}:
    => AnyT

@rank(0)
@name("Or{VoidT,Type...}")
reduce OrT{VoidT, Type... xs}:
    => OrT(xs)

// ====================================================================
// Function
// ====================================================================
term FunctionT[Type,Type, Type...]

// ====================================================================
// General Type
// ====================================================================

define Type as Atom | NotT | OrT | AndT | ArrayT | TupleT | FunctionT

